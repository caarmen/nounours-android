// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

allprojects {
    repositories {
        maven {
            url "https://dl.google.com/dl/android/maven2/"
        }
        jcenter()
        mavenLocal()
    }
    // Only show real releases with the ben-manes plugin.
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
    configurations.all {
       resolutionStrategy {
           eachDependency { dep ->
                if (dep.requested.group == 'com.android.support') {
                    dep.useVersion "27.1.0"
                }
           }
       }
    }
}

ext.compileSdkVersion=27
ext.buildToolsVersion="28.0.1"
ext.versionName="3.4.5"
ext.versionCode=345
ext.targetSdkVersion=27
