// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply plugin: 'com.github.ben-manes.versions'

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.3.1'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
    }
}

allprojects {
    repositories {
        google()
        jcenter()
        mavenLocal()
    }
    // Only show real releases with the ben-manes plugin.
    dependencyUpdates.resolutionStrategy = {
        componentSelection { rules ->
            rules.all { ComponentSelection selection ->
                boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                    selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
                }
                if (rejected) {
                    selection.reject('Release candidate')
                }
            }
        }
    }
    configurations.all {
       resolutionStrategy {
           eachDependency { dep ->
                if (dep.requested.group == 'com.android.support') {
                    dep.useVersion "28.0.0"
                }
           }
       }
    }
}

ext.compileSdkVersion=30
ext.buildToolsVersion="28.0.3"
ext.handheldVersionName="3.5.0"
ext.handheldVersionCode=350
ext.wearVersionName="3.4.9"
ext.wearVersionCode=349
ext.targetSdkVersion=30
ext.libnounoursVersion="2.0.1"
