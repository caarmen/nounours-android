apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        wearAppUnbundled true
        minSdkVersion 3
        targetSdkVersion rootProject.targetSdkVersion
        versionName rootProject.handheldVersionName
        versionCode rootProject.handheldVersionCode
        archivesBaseName += "-" + versionName
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "character"
    productFlavors {
        full {
            applicationId "ca.rmen.nounours"
            resValue "string", "DEFAULT_APP_THEME_ID", "5000"
            resValue "string", "DEFAULT_LWP_THEME_ID", "5001"
        }

        bugdroid {
            applicationId "ca.rmen.nounours.lwp.robot"
            resValue "string", "DEFAULT_APP_THEME_ID", "5004"
            resValue "string", "DEFAULT_LWP_THEME_ID", "5004"
        }

        all {
            resValue "string", "APPLICATION_ID", applicationId
        }
    }

    // https://code.google.com/p/android/issues/detail?id=152198
    // lint will complain about the resValue strings not being translated.
    lintOptions {
        // Set this issue to 'error' severity instead of 'fatal' severity.
        error 'MissingTranslation'
    }

    sourceSets {
        full {
            assets.srcDirs = ['src/bugdroid/assets', 'src/nounours/assets']
            res.srcDirs = ['src/full/res']
        }
    }

    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }

}

dependencies {
    implementation "ca.rmen:libnounours:$libnounoursVersion"
    implementation 'com.robbypond:android-ColorPickerPreference:1.11.1'
    implementation project(':common')
}
