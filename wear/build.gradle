apply plugin: 'com.android.application'


android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "ca.rmen.nounours"
        minSdkVersion 21
        targetSdkVersion rootProject.targetSdkVersion
        versionName rootProject.versionName
        versionCode rootProject.versionCode
        archivesBaseName += "-" + versionName
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        full {
            assets.srcDirs = ['src/bugdroid/assets', 'src/nounours/assets']
            res.srcDirs = ['src/bugdroid/res', 'src/nounours/res', 'src/main/res', 'src/full/res']
            java.srcDirs = ['src/bugdroid/java', 'src/nounours/java', 'src/main/java', 'src/full/java']
        }
    }

    publishNonDefault true
    productFlavors {
        full {
            applicationId "ca.rmen.nounours"
        }

        bugdroid {
            applicationId "ca.rmen.nounours.lwp.robot"
        }

        nounours {
            applicationId "ca.rmen.nounours.lwp.rainbow"
        }
    }


    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }
}

dependencies {
    compile 'com.google.android.support:wearable:1.4.0'
    compile 'com.google.android.gms:play-services-wearable:9.8.0'
    compile 'me.denley.wearpreferenceactivity:wearpreferenceactivity:0.5.0'
    compile project(':common')
    testCompile 'junit:junit:4.12'
    testCompile "org.robolectric:robolectric:3.2.2"
}

