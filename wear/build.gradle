apply plugin: 'com.android.application'


repositories {
    maven {
        url "https://dl.google.com/dl/android/maven2/"
    }
} 

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion

    defaultConfig {
        applicationId "ca.rmen.nounours"
        minSdkVersion 23
        targetSdkVersion rootProject.targetSdkVersion
        versionName rootProject.wearVersionName
        versionCode rootProject.wearVersionCode
        archivesBaseName += "-" + versionName
    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    flavorDimensions "character"
    productFlavors {
        nounours {
            applicationId "ca.rmen.nounours"
        }

        bugdroid {
            applicationId "ca.rmen.nounours.lwp.robot"
        }
    }

    File signFile = rootProject.file('signing.properties')
    if (signFile.exists()) {
        Properties p = new Properties();
        p.load(new FileInputStream(signFile))

        signingConfigs {
            release {
                keyAlias p.KeyAlias
                keyPassword p.KeyPassword
                storeFile file(p.StoreFile)
                storePassword p.StorePassword
            }
        }
        android.buildTypes.release.signingConfig =signingConfigs.release
    } else  {
        android.buildTypes.release.signingConfig = null
    }
}

dependencies {
    implementation 'com.google.android.support:wearable:2.4.0'
    compileOnly "com.google.android.wearable:wearable:2.4.0"
    implementation 'com.google.android.gms:play-services-wearable:16.0.1'
    implementation 'me.denley.wearpreferenceactivity:wearpreferenceactivity:0.5.0'
    implementation "ca.rmen:libnounours:$libnounoursVersion"
    implementation project(':common')
    testImplementation 'junit:junit:4.12'
    testImplementation "org.robolectric:robolectric:4.2"
}

